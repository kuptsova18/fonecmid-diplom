
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.РеализацияТоваровУслуг) Тогда
		
        КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
        КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.РеализацияТоваровУслуг.ПолноеИмя();
        КомандаСоздатьНаОсновании.Представление = ОбщегоНазначения.ПредставлениеОбъекта(Метаданные.Документы.РеализацияТоваровУслуг);
        КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции 

Процедура ПриОпределенииНастроекПечати(НастройкиОбъекта) Экспорт	
	НастройкиОбъекта.ПриДобавленииКомандПечати = Истина;
КонецПроцедуры

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Акт
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Акт";
	КомандаПечати.Представление = НСтр("ru = 'Акт'");
	КомандаПечати.Порядок = 5;	
КонецПроцедуры
    
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "Акт");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатнаяФормаАкта(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Акт'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.РеализацияТоваровУслуг.Макет";
	КонецЕсли;
		
КонецПроцедуры 

Функция ПечатнаяФормаАкта(МассивОбъектов, ОбъектыПечати)
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.КлючПараметровПечати = "Акт";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РеализацияТоваровУслуг.Макет");	
	ПервыйДокумент 			= Истина;
	ОбластьШапка 			= Макет.ПолучитьОбласть("ОбластьШапка");      
	ОбластьСтрока 			= Макет.ПолучитьОбласть("ОбластьСтрока");
	ОбластьИтого 			= Макет.ПолучитьОбласть("ОбластьИтогоЦифры");
	ОбластьИтогоПрописью 	= Макет.ПолучитьОбласть("ОбластьИтогоПрописью"); 
	ОбластьПодвал 			= Макет.ПолучитьОбласть("ОбластьПодвал");
	ПервыйДокумент = Истина;
	Для каждого Объект из МассивОбъектов Цикл
			Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
		ОбластьШапка.Параметры.Заполнить(Объект);
		ОбластьШапка.Параметры.Дата  = Формат(Объект.Дата,"ДФ=dd.MM.yyyy");  
		ТабДок.Вывести(ОбластьШапка);
 
		Для каждого стр из Объект.Услуги Цикл 
			ОбластьСтрока.Параметры.Заполнить(стр);
			ТабДок.Вывести(ОбластьСтрока);	
		КонецЦикла;  
		СуммаИтого = Объект.Услуги.Итог("Сумма");
		ОбластьИтого.Параметры.СуммаИтого = СуммаИтого;  
		ТабДок.Вывести(ОбластьИтого);   
		
		ФормСтрока = "Л = ru_RU; ДП = Истина";
		ПарПредмета="рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 2";
		ОбластьИтогоПрописью.Параметры.СуммаПрописью = ЧислоПрописью(СуммаИтого,ФормСтрока, ПарПредмета); 
		ТабДок.Вывести(ОбластьИтогоПрописью); 
		
		ТабДок.Вывести(ОбластьПодвал);
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, Объект.Ссылка);	
	КонецЦикла;
	Возврат ТабДок;	
КонецФункции  

#КонецОбласти

#КонецЕсли